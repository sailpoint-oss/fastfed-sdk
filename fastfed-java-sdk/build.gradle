plugins {
    // Apply the java plugin to add support for Java
    id 'java-library'
    id 'java-library-distribution'
    id 'maven-publish'
    id 'signing'
}

version = '0.3.0-SNAPSHOT'

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
    exclude('application.yml')
}

distributions {
    main {
        baseName = 'openid-fastfed-sdk'
        contents {
            from 'src/main/resources/'
            into "config"
        }
    }
}

configurations.all {
    exclude group: "org.slf4j", module: "log4j-over-slf4j"
}

configurations {
}

dependencies {
    compile group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.11.1'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.3'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'

    // json-simple should be replaced with nimbusds
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    compile("com.onelogin:java-saml:${project.JAVA_SAML_VERSION}")
    compile("com.auth0:java-jwt:${project.JAVA_JWT_VERSION}")
    compile("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${project.YAML_DATAFORMAT_VERSION}")
    compile("org.yaml:snakeyaml:${project.SNAKE_YAML_VERSION}")
    // slf4j
    // org.apache.commons.lang3.ObjectUtils;

    // replace this with nimbusds
    implementation("com.auth0:jwks-rsa:${project.JWKS_RSA_VERSION}")
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

java {
    modularity.inferModulePath = true
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = 'openid-fastfed-sdk'
                description = 'OpenID Fast Federation SDK for Java'
                url = 'http://github.com/sailpoint-oss/fastfed-sdk'

                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'brian-rose-sp'
                        name = 'Brian Rose'
                        email = 'brian.rose@sailpoint.com'
                    }
                    developer {
                        id = 'matt-domsch-sp'
                        name = 'Matt Domsch'
                        email = 'matt.domsch@sailpoint.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/sailpoint-oss/fastfed-sdk.git'
                    developerConnection = 'scm:git:ssh://github.com/sailpoint-oss/fastfed-sdk.git'
                    url = 'https://github.com/sailpoint-oss/fastfed-sdk/'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'myRepo'
            url = "file://${buildDir}/repo"
        }
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.maven
}

group = 'com.sailpoint'
sourceCompatibility = 9;
targetCompatibility = 9;
